
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}
apply plugin: 'forge'

sourceCompatibility = 7
targetCompatibility = 7

ext.configFile = file "version.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

ext.mc_version = "1.7.10"
ext.forge_version = "10.13.4.1614-1.7.10"
version = "0.1.1" + "." + config.build
group= "patrick96.friendlyfier" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "friendlyfier-" + mc_version

def saveVersion() {
    configFile.withWriter {
        config.toProperties().store(it, "")
    }
}

tasks.build.dependsOn 'bumpBuild'

task bumpBuild() << {
    config.build = Integer.parseInt(config.build) + 1
    saveVersion()
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "minecraft"
}

cleanProcessResources.execute()

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
